Перем юТест;
Перем Прокси;

////////////////////////////////////////////////////////////////////
// Программный интерфейс

Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	
	юТест = ЮнитТестирование;
	
	ВсеТесты = Новый Массив;

	Если Не ВРЕГ(ПеременныеСреды()["CI"]) = "TRUE" Тогда

		ВсеТесты.Добавить("ТестДолжен_ПроверитьЧтоСоздается");
		ВсеТесты.Добавить("ТестДолжен_ПроверитьПередачуЧисел");
		ВсеТесты.Добавить("ТестДолжен_ПроверитьПередачуСтрок");
		ВсеТесты.Добавить("ТестДолжен_ПроверитьПередачуДат");
		ВсеТесты.Добавить("ТестДолжен_ПроверитьПередачуБулева");
		ВсеТесты.Добавить("ТестДолжен_ПроверитьПередачуОбъекта");

		ВсеТесты.Добавить("ТестДолжен_ПроверитьАвторизациюПодРазнымиПользователями");

	КонецЕсли;

	Возврат ВсеТесты;
	
КонецФункции

Процедура ТестДолжен_ПроверитьЧтоСоздается() Экспорт

	Определения = Новый WSОпределения("http://bl-3/httpservice/ws/echo.1cws?wsdl", "default");
	Прокси = Новый WSПрокси(Определения, "http://dmpas/echo", "EchoService", "EchoServiceSoap");
	Прокси.Пользователь = "default";

КонецПроцедуры

Процедура ТестДолжен_ПроверитьПередачуЧисел() Экспорт

	юТест.ПроверитьРавенство(Прокси.EchoNumber(1), 1, "Прокси.EchoNumber(1)");
	юТест.ПроверитьРавенство(Прокси.EchoNumber(1.2), 1.2, "Прокси.EchoNumber(1.2)");

	юТест.ПроверитьРавенство(Прокси.EchoNumber(-1), -1, "Прокси.EchoNumber(-1)");
	юТест.ПроверитьРавенство(Прокси.EchoNumber(-1.2), -1.2, "Прокси.EchoNumber(-1.2)");

	юТест.ПроверитьРавенство(Прокси.EchoFloat(1), 1, "Прокси.EchoFloat(1)");
	юТест.ПроверитьРавенство(Прокси.EchoFloat(1.2), 1.2, "Прокси.EchoFloat(1.2)");

	юТест.ПроверитьРавенство(Прокси.EchoFloat(-1), -1, "Прокси.EchoFloat(-1)");
	юТест.ПроверитьРавенство(Прокси.EchoFloat(-1.2), -1.2, "Прокси.EchoFloat(-1.2)");

КонецПроцедуры

Процедура ТестДолжен_ПроверитьПередачуСтрок() Экспорт

	юТест.ПроверитьРавенство(Прокси.EchoString("123"), "123", "Прокси.EchoString(""123"")");
	юТест.ПроверитьРавенство(Прокси.EchoString("123
	|456"), "123
	|456", "Прокси.EchoString(""123--456"")");
	юТест.ПроверитьРавенство(Прокси.EchoString("<&>"), "<&>", "Прокси.EchoString(""<&>"")");

КонецПроцедуры

Процедура ТестДолжен_ПроверитьПередачуДат() Экспорт
	
	юТест.ПроверитьРавенство(Прокси.EchoDateTime('20170405125958'), '20170405125958', "Прокси.EchoDateTime('20170405125958')");

КонецПроцедуры

Процедура ТестДолжен_ПроверитьПередачуБулева() Экспорт

	юТест.ПроверитьРавенство(Прокси.EchoBool(Истина), Истина, "Прокси.EchoBool(Истина)");
	юТест.ПроверитьРавенство(Прокси.EchoBool(Ложь), Ложь, "Прокси.EchoBool(Ложь)");

КонецПроцедуры

Процедура ТестДолжен_ПроверитьПередачуОбъекта() Экспорт
	
	ТипОбъекта     = Прокси.ФабрикаXDTO.Тип("http://example.com/soap-mixed", "Object");
	ОбъектПараметр = Прокси.ФабрикаXDTO.Создать(ТипОбъекта);
	юТест.ПроверитьРавенство(ТипЗнч(ОбъектПараметр), Тип("ОбъектXDTO"), "Создан тип объекта XDTO");
	ОбъектПараметр.Установить("BaseProperty", "base property value");
	ОбъектПараметр.Установить("Property", "property value"); 
	Результат = Прокси.EchoObject(ОбъектПараметр);
	юТест.ПроверитьНеРавенство(Результат, Неопределено, "Прокси.EchoObject(... param ...)");
	юТест.ПроверитьРавенство(Результат.BaseProperty, "base property value", "Прокси.EchoObject(... param ...)");
	юТест.ПроверитьРавенство(Результат.Property, "property value", "Прокси.EchoObject(... param ...)");
	
	Список = Результат.ПолучитьСписок("ListedProperty");
	Для Каждого мЭлементСписка Из Список Цикл
	
	    // Проверяем, что работает пробег циклом
	
    КонецЦикла;
    
	юТест.ПроверитьРавенство(2, Список.Количество(), "Прокси.EchoObject(... param ...).List.Count");
    юТест.ПроверитьРавенство(Список.Получить(0), "Text1", "Прокси.EchoObject(... param ...).List.Get[0]");
	юТест.ПроверитьРавенство(Список.Получить(1), "Text2", "Прокси.EchoObject(... param ...).List.Get[1]");
	
	юТест.ПроверитьРавенство(Список[0], "Text1", "Прокси.EchoObject(... param ...).List[0]");
	юТест.ПроверитьРавенство(Список[1], "Text2", "Прокси.EchoObject(... param ...).List[1]");
	
	юТест.ПроверитьРавенство(2, Результат.ListFromOtherPackage.Количество(), "Прокси.EchoObject(... param ...).ListFromOtherPackage.Count");
	Для Каждого мЭлементСписка Из Результат.ListFromOtherPackage Цикл
	
	    // Проверяем, что работает пробег циклом
	
    КонецЦикла;
	
    юТест.ПроверитьРавенство(Результат.ListFromOtherPackage.Получить(0).name, "123", "Прокси.EchoObject(... param ...).ListFromOtherPackage.Get[0]");
	юТест.ПроверитьРавенство(Результат.ListFromOtherPackage.Получить(1).name, "456", "Прокси.EchoObject(... param ...).ListFromOtherPackage.Get[1]");
	
	юТест.ПроверитьРавенство(Результат.ListFromOtherPackage[0].name, "123", "Прокси.EchoObject(... param ...).ListFromOtherPackage[0]");
	юТест.ПроверитьРавенство(Результат.ListFromOtherPackage[1].name, "456", "Прокси.EchoObject(... param ...).ListFromOtherPackage[1]");
	
КонецПроцедуры


Процедура ТестДолжен_ПроверитьАвторизациюПодРазнымиПользователями() Экспорт

	Прокси.Пользователь = "User1";
	юТест.ПроверитьРавенство(Прокси.WhoAmI(), "User1");

	Прокси.Пользователь = "User2";
	Прокси.Пароль = "User2";
	юТест.ПроверитьРавенство(Прокси.WhoAmI(), "User2");

КонецПроцедуры
